#!/bin/zsh

prompt_opts=(subst percent)

prompt_bartek_precmd() {
  autoload -Uz vcs_info 
  vcs_info  
  zstyle ':vcs_info:*'                  enable            git
  zstyle ':vcs_info:git:*'              stagedstr         "%F{green}+%f"
  zstyle ':vcs_info:git:*'              unstagedstr       "%F{magenta}*%f"
  zstyle ':vcs_info:git*+set-message:*' hooks             git-misc
  zstyle ':vcs_info:git:*'              check-for-changes true
  zstyle ':vcs_info:git:*'              formats           '%b%c%u%m%a'
}

+vi-git-misc() {
  local ahead behind
  local -a gitstatus
  
  ahead=$(git rev-list origin/${hook_com[branch]}..HEAD 2> /dev/null | wc -l)
  # ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
  (( $ahead )) && gitstatus+=( "%F{green}+${ahead}%f" )
  
  behind=$(git rev-list HEAD..origin/${hook_com[branch]} 2> /dev/null | wc -l)
  # behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
  (( $behind )) && gitstatus+=( "%F{red}-${behind}%f" )

  hook_com[misc]+=${(j:/:)gitstatus}

  if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && git status --porcelain | grep -m 1 '^??' &>/dev/null
  then
    hook_com[misc]+='%F{white}?%f'
  fi
}

git_info() {
  [ ! -z "$vcs_info_msg_0_" ] && echo "$vcs_info_msg_0_ "
}

prompt_bartek_setup() {  
  add-zsh-hook precmd prompt_bartek_precmd
  sign='%(?.%F{blue}Î»%f.%F{red}!%f)'
  PS1=$'%F{green}%1~ %F{yellow}$(git_info)$sign '
  RPS1='%F{green}%*%f %F{blue}@%F{yellow}%m%f '
}

prompt_bartek_setup $@
